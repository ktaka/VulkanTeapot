apply plugin: 'com.android.model.application'


model {
    android {
        compileSdkVersion 24
        buildToolsVersion = "23.0.2"

        defaultConfig {
            applicationId = "jp.kabuku.rd.vulkan.teapot"
            minSdkVersion.apiLevel = 24
            targetSdkVersion.apiLevel = 24
            versionCode = 1
            versionName = "0.0.1"
            // Shader compilation directives, put glsl shaders to app/src/main/shaders
            // android studio will pick them up and compile them into APK/assets/shaders
            shaders {
                glslcArgs.addAll(['-c', '-g'])
            }
        }

        ndk {
            moduleName = "vktuts"
            toolchain = "clang"
            stl = "gnustl_static"
            ldLibs.addAll(["log", "android"])
            cppFlags.addAll([ "-Werror","-std=c++11",
                              "-DVK_USE_PLATFORM_ANDROID_KHR"])
            cppFlags.addAll(["-I" + file("src/main/jni"),
                             "-I" + file("../../vulkan_wrapper")])
            abiFilters.addAll(["armeabi-v7a", 'arm64-v8a', 'x86', 'x86_64'])
        }
        // Turn on hard float support in armeabi-v7a
        abis {
            create("armeabi-v7a") {
                cppFlags.addAll(["-mhard-float", "-D_NDK_MATH_NO_SOFTFP=1", "-mfloat-abi=hard"])
                ldLibs.add("m_hard")
                ldFlags.add("-Wl,--no-warn-mismatch")
            }
        }
        sources {
            main {
                jni {
                    dependencies {
                        project ":nativeactivity" linkage "static"
                    }
                    source {
                        srcDir 'src/main/jni'
                        srcDir '../../vulkan_wrapper'
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file('proguard-rules.pro'))
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':nativeactivity')
}
